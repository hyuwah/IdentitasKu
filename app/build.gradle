apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: "org.jlleitschuh.gradle.ktlint"

def versionMajor = 1
def versionMinor = 2
def versionPatch = 0
def versionBuild = 0

android {
    signingConfigs {
        debug {

        }
        release {
            Properties props = new Properties()
            props.load(project.rootProject.file('local.properties').newDataInputStream())
            keyAlias props.getProperty('keyAlias')
            keyPassword props.getProperty('keyPassword')
            storeFile file(props.getProperty('storeFile'))
            storePassword props.getProperty('storePassword')
        }
    }
    compileSdk 33
    defaultConfig {
        applicationId "com.muhammadwahyudin.identitasku"
        minSdk 21
        targetSdk 33
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName = "identitasku_" + variant.versionName + ".apk"
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            manifestPlaceholders = [appName: "IdentitasKu-dev", crashlyticsEnabled: true]
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "IdentitasKu", crashlyticsEnabled: true]
        }
    }
    buildFeatures {
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
    namespace 'com.muhammadwahyudin.identitasku'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Android Jetpack
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    // KTX
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "androidx.fragment:fragment-ktx:1.6.1"

    // Coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    implementation 'androidx.room:room-runtime:2.5.2'
    kapt "androidx.room:room-compiler:2.5.2"
    implementation "androidx.room:room-ktx:2.5.2"

    // Lifecycle Arch
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-common-java8:2.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"


    // DI
    implementation "org.koin:koin-android:2.1.5"
    implementation 'org.koin:koin-android-viewmodel:2.1.5'
    // Firebase
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'com.google.firebase:firebase-analytics-ktx:21.3.0'

    // Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics:18.4.1'
    // Chrome custom tab
    implementation 'androidx.browser:browser:1.5.0'
    // Image
    implementation "io.coil-kt:coil:2.3.0"
    // Language
    implementation "com.github.YarikSOffice:lingver:1.2.2"
    // Log
    implementation 'com.jakewharton.timber:timber:4.7.1'
    // Pref & Storage
    implementation 'com.orhanobut:hawk:2.0.1'
    implementation 'com.commonsware.cwac:saferoom.x:1.0.1'
    implementation 'com.facebook.stetho:stetho:1.5.1'
    // Process
    implementation 'com.jakewharton:process-phoenix:2.0.0'
    // Custom View
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.3'
    implementation 'com.airbnb.android:lottie:5.2.0'
    implementation 'com.vinaygaba:creditcardview:1.0.4'
    implementation 'com.afollestad:vvalidator:0.5.2'
    implementation 'com.link184:kid-adapter:1.3.6'
    //ShowcaseView
    implementation 'com.github.iammert:MaterialIntroView:5e0c6b6'
    // https://github.com/iammert/MaterialIntroView/issues/84
    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

ktlint {
    verbose = true
    android = true
    outputToConsole = true
    outputColorName = "RED"
    disabledRules = [
            "final-newline",
            "no-wildcard-imports",
            "import-ordering"
    ]
    reporters {
        reporter "plain"
        reporter "checkstyle"
    }
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}
