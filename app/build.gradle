apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.kotlin.android.extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'
apply plugin: "org.jlleitschuh.gradle.ktlint"

def versionMajor = 1
def versionMinor = 1
def versionPatch = 0
def versionBuild = 1

android {
    signingConfigs {
        release {
            Properties props = new Properties()
            props.load(project.rootProject.file('local.properties').newDataInputStream())
            keyAlias props.getProperty('keyAlias')
            keyPassword props.getProperty('keyPassword')
            storeFile file(props.getProperty('storeFile'))
            storePassword props.getProperty('storePassword')
        }
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.muhammadwahyudin.identitasku"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName = "identitasku_" + variant.versionName + ".apk"
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            zipAlignEnabled false
            minifyEnabled false
            manifestPlaceholders = [appName: "IdentitasKu-dev", crashlyticsEnabled: true]
        }
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "IdentitasKu", crashlyticsEnabled: true]
        }
    }
    viewBinding {
        enabled = true
    }
    androidExtensions {
        experimental = true
    }
    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Android Jetpack
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.2.0-alpha06'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    // KTX
    implementation 'androidx.core:core-ktx:1.3.0-rc01'
    implementation "androidx.fragment:fragment-ktx:1.2.4"

    // Coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'

    implementation 'androidx.room:room-runtime:2.2.5'
    kapt "androidx.room:room-compiler:2.2.5"
    implementation "androidx.room:room-ktx:2.2.5"

    // Lifecycle Arch
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"


    // DI
    implementation "org.koin:koin-android:2.1.5"
    implementation 'org.koin:koin-android-viewmodel:2.1.5'
    // Firebase
    implementation 'com.google.firebase:firebase-core:17.4.0'
    implementation 'com.google.firebase:firebase-analytics:17.4.0'
    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    // Chrome custom tab
    implementation 'androidx.browser:browser:1.2.0'
    //    implementation 'com.github.bumptech.glide:glide:4.9.0'
    // Language
    implementation "com.github.YarikSOffice:lingver:1.2.2"
    // Log
    implementation 'com.jakewharton.timber:timber:4.7.1'
    // Pref & Storage
    implementation 'com.orhanobut:hawk:2.0.1'
    implementation 'com.commonsware.cwac:saferoom.x:1.0.1'
    implementation 'com.facebook.stetho:stetho:1.5.1'
    // Process
    implementation 'com.jakewharton:process-phoenix:2.0.0'
    // Custom View
    implementation 'com.toptoche.searchablespinner:searchablespinnerlibrary:1.3.1'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.3'
    implementation 'com.airbnb.android:lottie:3.3.0'
    implementation 'com.vinaygaba:creditcardview:1.0.4'
    //ShowcaseView
    implementation 'com.github.iammert:MaterialIntroView:5e0c6b6' // https://github.com/iammert/MaterialIntroView/issues/84
    // Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

ktlint {
    verbose = true
    android = true
    outputToConsole = true
    outputColorName = "RED"
    disabledRules = [
            "final-newline",
            "no-wildcard-imports",
            "import-ordering"
    ]
    reporters {
        reporter "plain"
        reporter "checkstyle"
    }
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}