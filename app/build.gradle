apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.kotlin.android.extensions'
apply plugin: 'io.fabric'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 2
def versionBuild = 0

def getBuildDate() {
    def date = new Date()
    def formattedDate = date.format("yyyy-MM-dd")
    return formattedDate
}
def BUILD_DATE = getBuildDate()

android {
    signingConfigs {
        release {
            Properties props = new Properties()
            props.load(project.rootProject.file('local.properties').newDataInputStream())
            keyAlias props.getProperty('keyAlias')
            keyPassword props.getProperty('keyPassword')
            storeFile file(props.getProperty('storeFile'))
            storePassword props.getProperty('storePassword')
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.muhammadwahyudin.identitasku"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug" + " (" + BUILD_DATE + ")"
            debuggable true
            zipAlignEnabled false
            minifyEnabled false
            manifestPlaceholders = [appName: "IdentitasKu-dev", crashlyticsEnabled: false]
        }
        release {
            versionNameSuffix " (" + BUILD_DATE + ")"
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "IdentitasKu", crashlyticsEnabled: true]

        }
    }
    dataBinding {
        enabled = true
    }
    androidExtensions {
        experimental = true
    }
    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Android Jetpack
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.0-alpha04'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.room:room-runtime:2.1.0-alpha06'
    kapt "androidx.room:room-compiler:2.1.0-alpha06"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-alpha04'
    // KTX
    implementation 'androidx.core:core-ktx:1.1.0-alpha05'
    // Coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.0-alpha-2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.0-alpha-2'
    // DI
    implementation 'org.kodein.di:kodein-di-generic-jvm:5.2.0'
    implementation 'org.kodein.di:kodein-di-framework-android-x:5.2.0'
    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.8'
    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    // ANKO
    implementation "org.jetbrains.anko:anko:$anko_version"
    // Chrome custom tab
    implementation 'androidx.browser:browser:1.0.0'
    // Fingerprint
    implementation 'com.github.pwittchen:rxbiometric:0.1.0'
    //    implementation 'com.github.bumptech.glide:glide:4.9.0'
    // Log
    implementation 'com.jakewharton.timber:timber:4.7.1'
    // Pref & Storage
    implementation 'com.orhanobut:hawk:2.0.1'
    implementation 'com.commonsware.cwac:saferoom.x:1.0.1'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    // Custom View
    implementation 'com.toptoche.searchablespinner:searchablespinnerlibrary:1.3.1'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46'
    implementation 'com.airbnb.android:lottie:3.0.0'
    implementation 'com.vinaygaba:creditcardview:1.0.4'
    // Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0-alpha03'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha03'
}

apply plugin: 'com.google.gms.google-services'
